plugins {
    id 'com.android.library'
    id 'maven-publish' // Publishing code taken from https://stackoverflow.com/a/42160584
}

android {
    compileSdkVersion 32

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 32
        versionName "2c9ba872b2" // the version that dependents will use

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
    publishing {
        singleVariant("release") {
            // if you don't want sources/javadoc, remove these lines
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    implementation 'com.github.singularity-group:js-evaluator-for-android:cc8c7fe371'
    implementation 'androidx.annotation:annotation:1.4.0'

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test:rules:1.4.0'
}

// To publish, run ./gradlew :youtubeExtractor:publishAllPublicationsToMyrepoRepository
// (then upload files to static file hosting)
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId 'com.github.singularity-group'
                artifactId 'android-youtubeExtractor'
                version = android.defaultConfig.versionName
            }
        }
        repositories {
            repositories {
                maven {
                    name = 'myrepo'
                    url = "${project.buildDir}/repo"
                }
            }
        }
    }
}
